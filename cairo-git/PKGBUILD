# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cairo
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=git
pkgrel=0
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=('any')
url="http://cairographics.org"
license=("LGPL, MPL")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
    "${MINGW_PACKAGE_PREFIX}-pkg-config"
    "${MINGW_PACKAGE_PREFIX}-glib2"
    "${MINGW_PACKAGE_PREFIX}-librsvg"
    "${MINGW_PACKAGE_PREFIX}-gtk2"
    "${MINGW_PACKAGE_PREFIX}-poppler"
    "${MINGW_PACKAGE_PREFIX}-ghostscript"
    )
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
    "${MINGW_PACKAGE_PREFIX}-freetype"
    "${MINGW_PACKAGE_PREFIX}-fontconfig"
    "${MINGW_PACKAGE_PREFIX}-lzo2"
    "${MINGW_PACKAGE_PREFIX}-zlib"
    "${MINGW_PACKAGE_PREFIX}-pixman")
optdepends=("mingw-w64-glib2: libcairo-gobject")
options=('!strip' 'debug' 'staticlibs')
source=("git://anongit.freedesktop.org/cairo"
        0009-standalone-headers.mingw.patch
        0013-mozilla-transparent-windows.all.patch
        0022-dont-fail-argb-similar.all.patch
        0026-create-argb-fonts.all.patch
        0027-Bug-53121-hack-around.all.patch)
sha1sums=('SKIP'
          '58c548d2791ba20dd7f6e328ff596f746df3aa10'
	  'b4b8c6f4931814e7e77ddeb98fecaf70cb807e04'
	  'fa601d6d2b2c75b0b1608ab3fa9c2e5b6cc31c9e'
	  '9c0e533614782a41af2c3806a43ab7fe9d6a5431'
	  'b74c9998d960a64fbf606ebe0284187add0da55a')

prepare() {
  cd "${srcdir}"/${_realname}
  patch -p1 -i ${srcdir}/0009-standalone-headers.mingw.patch
  patch -p1 -i ${srcdir}/0013-mozilla-transparent-windows.all.patch
  patch -p1 -i ${srcdir}/0022-dont-fail-argb-similar.all.patch
  patch -p1 -i ${srcdir}/0026-create-argb-fonts.all.patch
  patch -p1 -i ${srcdir}/0027-Bug-53121-hack-around.all.patch
  
  "${srcdir}"/${_realname}/autogen.sh
}

build() {
  export lt_cv_deplibs_check_method='pass_all'
  export ax_cv_c_float_words_bigendian=no
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  CFLAGS+=" -fno-lto"
  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-win32 \
    --enable-win32-font \
    --enable-png \
    --enable-shared \
    --enable-static \
    --enable-gobject \
    --enable-tee \
    --disable-xlib \
    --disable-xcb \
    --enable-fc \
    --enable-ft \
    --disable-silent-rules \
    ac_cv_prog_GS=${MINGW_PREFIX}/bin/gsc
  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="$pkgdir" install
}
